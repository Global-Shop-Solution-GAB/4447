Program.Sub.ScreenSU.Start
Gui.Form_ParentExlusion..Create
Gui.Form_ParentExlusion..Caption("Bom Part Exclusion")
Gui.Form_ParentExlusion..Size(4635,10695)
Gui.Form_ParentExlusion..MinX(0)
Gui.Form_ParentExlusion..MinY(0)
Gui.Form_ParentExlusion..Position(0,0)
Gui.Form_ParentExlusion..BackColor(-2147483633)
Gui.Form_ParentExlusion..MousePointer(0)
Gui.Form_ParentExlusion..Event(UnLoad,Form_ParentExlusion_UnLoad)
Gui.Form_ParentExlusion.GsGridControl1.Create(GsGridControl)
Gui.Form_ParentExlusion.GsGridControl1.Size(4290,9360)
Gui.Form_ParentExlusion.GsGridControl1.Position(105,735)
Gui.Form_ParentExlusion.GsGridControl1.Event(MouseCellEnter,GsGridControl1_MouseCellEnter)
Gui.Form_ParentExlusion.txtPart.Create(TextBox,"",True,1920,300,0,105,360,True,0,"Arial",8,-2147483643,1)
Gui.Form_ParentExlusion.txtPart.MaxLength(20)
Gui.Form_ParentExlusion.txtPart.Event(GotFocus,txtPart_GotFocus)
Gui.Form_ParentExlusion.txtPart.Event(LostFocus,txtPart_LostFocus)
Gui.Form_ParentExlusion.txtRev.Create(TextBox,"",True,600,300,0,2100,360,True,0,"Arial",8,-2147483643,1)
Gui.Form_ParentExlusion.txtRev.MaxLength(3)
Gui.Form_ParentExlusion.txtRev.Event(GotFocus,txtRev_GotFocus)
Gui.Form_ParentExlusion.txtRev.Event(LostFocus,txtRev_LostFocus)
Gui.Form_ParentExlusion.cmdAdd.Create(Button)
Gui.Form_ParentExlusion.cmdAdd.Size(855,375)
Gui.Form_ParentExlusion.cmdAdd.Position(3315,330)
Gui.Form_ParentExlusion.cmdAdd.Caption("Add")
Gui.Form_ParentExlusion.cmdAdd.Event(Click,cmdAdd_Click)
Gui.Form_ParentExlusion.lblPart.Create(Label,"Part",True,1845,255,0,105,120,True,0,"Arial",8,-2147483633,0)
Gui.Form_ParentExlusion.lblRev.Create(Label,"Rev",True,570,255,0,2100,120,True,0,"Arial",8,-2147483633,0)
Gui.Form_ParentExlusion.cmdPart.Create(Button)
Gui.Form_ParentExlusion.cmdPart.Size(420,375)
Gui.Form_ParentExlusion.cmdPart.Position(2805,330)
Gui.Form_ParentExlusion.cmdPart.Caption("^")
Gui.Form_ParentExlusion.cmdPart.Event(Click,cmdPart_Click)
Program.Sub.ScreenSU.End

Program.Sub.Preflight.Start
V.Global.iIndex.Declare(Long,-1)
Program.Sub.Preflight.End

Program.Sub.Main.Start
F.Intrinsic.Control.Try
	V.Local.sError.Declare(String,"")
	
	'coded by RLK 5/31/2018
	'program will allow user to enter bom parent/rev that will be excluded in the bom upload.
	'entered in parts will automatically select the bom and parts that's already in GSS and not update from the bom import file
	
	'create context menu
	F.Intrinsic.Control.CallSub(Create_ContextMenu)
	'get data
	F.Intrinsic.Control.CallSub(Get_Data)
	'set properties
	F.Intrinsic.Control.CallSub(Set_Properties)
	
	F.Intrinsic.Control.ExitSub

F.Intrinsic.Control.Catch
	F.Intrinsic.Control.If(V.Ambient.ErrorNumber,<>,0)
		F.Intrinsic.String.Build("Project: {0}{1}Subroutine: {2}{1}Error Occurred {3} with description {4}","GCG_4447_Bom_Part_Exclusion.g2u",V.Ambient.NewLine,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Local.sError)
		F.Intrinsic.UI.Msgbox(V.Local.sError)
		F.Intrinsic.Control.CallSub(Form_ParentExlusion_UnLoad)
	F.Intrinsic.Control.EndIf
Function.Intrinsic.Control.EndTry
Program.Sub.Main.End

Program.Sub.Form_ParentExlusion_UnLoad.Start
F.Intrinsic.Control.Try
	V.Local.sError.Declare(String,"")

	'checking if connection is open before closing program
	F.Intrinsic.Control.If(V.ODBC.Con.Exists,=,1)
		'closing connection
		F.ODBC.Connection!Con.Close
	F.Intrinsic.Control.EndIf
	'ending program
	F.Intrinsic.Control.End
	
	F.Intrinsic.Control.ExitSub

F.Intrinsic.Control.Catch
	F.Intrinsic.Control.If(V.Ambient.ErrorNumber,<>,0)
		F.Intrinsic.String.Build("Project: {0}{1}Subroutine: {2}{1}Error Occurred {3} with description {4}","GCG_4447_Bom_Part_Exclusion.g2u",V.Ambient.NewLine,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Local.sError)
		F.Intrinsic.UI.Msgbox(V.Local.sError)
		F.Intrinsic.Control.End
	F.Intrinsic.Control.EndIf
Function.Intrinsic.Control.EndTry
Program.Sub.Form_ParentExlusion_UnLoad.End

Program.Sub.txtRev_GotFocus.Start
F.Intrinsic.Control.Try
	V.Local.sError.Declare(String,"")
	'selecting all text
	Gui.Form_ParentExlusion.txtRev.SelectAll
	
	F.Intrinsic.Control.ExitSub

F.Intrinsic.Control.Catch
	F.Intrinsic.Control.If(V.Ambient.ErrorNumber,<>,0)
		F.Intrinsic.String.Build("Project: {0}{1}Subroutine: {2}{1}Error Occurred {3} with description {4}","GCG_4447_Bom_Part_Exclusion.g2u",V.Ambient.NewLine,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Local.sError)
		F.Intrinsic.UI.Msgbox(V.Local.sError)
		F.Intrinsic.Control.CallSub(Form_ParentExlusion_UnLoad)
	F.Intrinsic.Control.EndIf
Function.Intrinsic.Control.EndTry
Program.Sub.txtRev_GotFocus.End

Program.Sub.txtRev_LostFocus.Start
F.Intrinsic.Control.Try
	V.Local.sError.Declare(String,"")
	'selecting all text
	Gui.Form_ParentExlusion.cmdAdd.SetFocus
	
	F.Intrinsic.Control.ExitSub

F.Intrinsic.Control.Catch
	F.Intrinsic.Control.If(V.Ambient.ErrorNumber,<>,0)
		F.Intrinsic.String.Build("Project: {0}{1}Subroutine: {2}{1}Error Occurred {3} with description {4}","GCG_4447_Bom_Part_Exclusion.g2u",V.Ambient.NewLine,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Local.sError)
		F.Intrinsic.UI.Msgbox(V.Local.sError)
		F.Intrinsic.Control.CallSub(Form_ParentExlusion_UnLoad)
	F.Intrinsic.Control.EndIf
Function.Intrinsic.Control.EndTry
Program.Sub.txtRev_LostFocus.End

Program.Sub.cmdAdd_Click.Start
F.Intrinsic.Control.Try
	V.Local.sError.Declare(String,"")
	V.Local.sFilter.Declare(String,"")
	V.Local.sRet.Declare(String,"")
	
	'opening company connection
	F.ODBC.Connection!Con.OpenCompanyConnection
	'checking that a part has been entered
	F.Intrinsic.Control.If(V.Screen.Form_ParentExlusion!txtPart.Text,<>,"")
		'checking if the part/rev is already in the datatable
		F.Intrinsic.String.Build("Part = '{0}' and Rev = '{1}'",V.Screen.Form_ParentExlusion!txtPart.Text,V.Screen.Form_ParentExlusion!txtRev.Text,V.Local.sFilter)
		F.Data.DataTable.Select("Part",V.Local.sFilter,V.Local.sRet)
		'if part doesn't exist add to datatable
		F.Intrinsic.Control.If(V.Local.sRet,=,"***NORETURN***")
			F.Data.DataTable.AddRow("Part","Part",V.Screen.Form_ParentExlusion!txtPart.Text,"Rev",V.Screen.Form_ParentExlusion!txtRev.Text)
			F.Data.DataTable.SaveToDB("Part","Con","GCG_4447_Bom_Prt_Exc","Part*!*Rev",7,"Part@!@Part*!*Rev@!@Rev")
			F.Data.DataTable.AcceptChanges("Part")
		F.Intrinsic.Control.EndIf
	F.Intrinsic.Control.EndIf
	'closing connection
	F.ODBC.Connection!Con.Close
	
	F.Intrinsic.Control.ExitSub

F.Intrinsic.Control.Catch
	F.Intrinsic.Control.If(V.Ambient.ErrorNumber,<>,0)
		F.Intrinsic.String.Build("Project: {0}{1}Subroutine: {2}{1}Error Occurred {3} with description {4}","GCG_4447_Bom_Part_Exclusion.g2u",V.Ambient.NewLine,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Local.sError)
		F.Intrinsic.UI.Msgbox(V.Local.sError)
		F.Intrinsic.Control.CallSub(Form_ParentExlusion_UnLoad)
	F.Intrinsic.Control.EndIf
Function.Intrinsic.Control.EndTry
Program.Sub.cmdAdd_Click.End

Program.Sub.txtPart_GotFocus.Start
F.Intrinsic.Control.Try
	V.Local.sError.Declare(String,"")
	'select all text
	Gui.Form_ParentExlusion.txtPart.SelectAll
	
	F.Intrinsic.Control.ExitSub

F.Intrinsic.Control.Catch
	F.Intrinsic.Control.If(V.Ambient.ErrorNumber,<>,0)
		F.Intrinsic.String.Build("Project: {0}{1}Subroutine: {2}{1}Error Occurred {3} with description {4}","GCG_4447_Bom_Part_Exclusion.g2u",V.Ambient.NewLine,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Local.sError)
		F.Intrinsic.UI.Msgbox(V.Local.sError)
		F.Intrinsic.Control.CallSub(Form_ParentExlusion_UnLoad)
	F.Intrinsic.Control.EndIf
Function.Intrinsic.Control.EndTry
Program.Sub.txtPart_GotFocus.End

Program.Sub.txtPart_LostFocus.Start
F.Intrinsic.Control.Try
	V.Local.sError.Declare(String,"")
	V.Local.sPart.Declare(String,"")
	V.Local.sRev.Declare(String,"")
	
	'If user typed in the part/rev in the part text box splitting the two apart and setting the rev text box
	F.Intrinsic.String.RPad(V.Screen.Form_ParentExlusion!txtPart.Text," ",20,V.Local.sPart)
	F.Intrinsic.String.Right(V.Local.sPart,3,V.Local.sRev)
	F.Intrinsic.String.Left(V.Local.sPart,17,V.Local.sPart)
	
	Gui.Form_ParentExlusion.txtPart.Text(V.Local.sPart.Trim)
	Gui.Form_ParentExlusion.txtRev.Text(V.Local.sRev.Trim)
	
	Gui.Form_ParentExlusion.txtRev.SetFocus
	
	F.Intrinsic.Control.ExitSub

F.Intrinsic.Control.Catch
	F.Intrinsic.Control.If(V.Ambient.ErrorNumber,<>,0)
		F.Intrinsic.String.Build("Project: {0}{1}Subroutine: {2}{1}Error Occurred {3} with description {4}","GCG_4447_Bom_Part_Exclusion.g2u",V.Ambient.NewLine,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Local.sError)
		F.Intrinsic.UI.Msgbox(V.Local.sError)
		F.Intrinsic.Control.CallSub(Form_ParentExlusion_UnLoad)
	F.Intrinsic.Control.EndIf
Function.Intrinsic.Control.EndTry
Program.Sub.txtPart_LostFocus.End

Program.Sub.Get_Data.Start
F.Intrinsic.Control.Try
	V.Local.sError.Declare(String,"")
	V.Local.sSql.Declare(String,"")
	
	'opening company connection
	F.ODBC.Connection!Con.OpenCompanyConnection
	'getting part/rev from custom table
	V.Local.sSql.Set("Select Part, Rev From GCG_4447_Bom_Prt_Exc Order by Part,Rev")
	'creating datatable
	F.Data.DataTable.CreateFromSQL("Part","con",V.Local.sSql,True)
	'closing connection
	F.ODBC.Connection!Con.Close
	
	F.Intrinsic.Control.ExitSub

F.Intrinsic.Control.Catch
	F.Intrinsic.Control.If(V.Ambient.ErrorNumber,<>,0)
		F.Intrinsic.String.Build("Project: {0}{1}Subroutine: {2}{1}Error Occurred {3} with description {4}","GCG_4447_Bom_Part_Exclusion.g2u",V.Ambient.NewLine,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Local.sError)
		F.Intrinsic.UI.Msgbox(V.Local.sError)
		F.Intrinsic.Control.CallSub(Form_ParentExlusion_UnLoad)
	F.Intrinsic.Control.EndIf
Function.Intrinsic.Control.EndTry
Program.Sub.Get_Data.End

Program.Sub.Set_Properties.Start
F.Intrinsic.Control.Try
	V.Local.sError.Declare(String,"")
	'setting properties
	Gui.Form_ParentExlusion.GsGridControl1.AddGridviewFromDatatable("GridPart","Part")
	
	Gui.Form_ParentExlusion.GsGridControl1.SetGridviewProperty("GridPart","Editable",False)
	Gui.Form_ParentExlusion.GsGridControl1.SetGridviewProperty("GridPart","ReadOnly",True)
	
	Gui.Form_ParentExlusion.GsGridControl1.SetColumnProperty("GridPart","Part","HeaderBackColor","Green")
	Gui.Form_ParentExlusion.GsGridControl1.SetColumnProperty("GridPart","Rev","HeaderBackColor","Green")
	Gui.Form_ParentExlusion.GsGridControl1.SetColumnProperty("GridPart","Part","HeaderForeColor","White")
	Gui.Form_ParentExlusion.GsGridControl1.SetColumnProperty("GridPart","Rev","HeaderForeColor","White")
	'attaching gridview to grid
	Gui.Form_ParentExlusion.GsGridControl1.MainView("GridPart")
	'showing form
	Gui.Form_ParentExlusion..Show
	
	F.Intrinsic.Control.ExitSub

F.Intrinsic.Control.Catch
	F.Intrinsic.Control.If(V.Ambient.ErrorNumber,<>,0)
		F.Intrinsic.String.Build("Project: {0}{1}Subroutine: {2}{1}Error Occurred {3} with description {4}","GCG_4447_Bom_Part_Exclusion.g2u",V.Ambient.NewLine,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Local.sError)
		F.Intrinsic.UI.Msgbox(V.Local.sError)
		F.Intrinsic.Control.CallSub(Form_ParentExlusion_UnLoad)
	F.Intrinsic.Control.EndIf
Function.Intrinsic.Control.EndTry
Program.Sub.Set_Properties.End

Program.Sub.Set_Anchors.Start
F.Intrinsic.Control.Try
	V.Local.sError.Declare(String,"")
	'setting anchors
	Gui.Form_ParentExlusion.lblPart.Anchor(5)
	Gui.Form_ParentExlusion.lblRev.Anchor(5)
	Gui.Form_ParentExlusion.txtPart.Anchor(5)
	Gui.Form_ParentExlusion.txtRev.Anchor(5)
	Gui.Form_ParentExlusion.GsGridControl1.Anchor(15)
	
	F.Intrinsic.Control.ExitSub

F.Intrinsic.Control.Catch
	F.Intrinsic.Control.If(V.Ambient.ErrorNumber,<>,0)
		F.Intrinsic.String.Build("Project: {0}{1}Subroutine: {2}{1}Error Occurred {3} with description {4}","GCG_4447_Bom_Part_Exclusion.g2u",V.Ambient.NewLine,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Local.sError)
		F.Intrinsic.UI.Msgbox(V.Local.sError)
		F.Intrinsic.Control.CallSub(Form_ParentExlusion_UnLoad)
	F.Intrinsic.Control.EndIf
Function.Intrinsic.Control.EndTry
Program.Sub.Set_Anchors.End

Program.Sub.cmdPart_Click.Start
F.Intrinsic.Control.Try
	V.Local.sError.Declare(String,"")
	V.Local.sRet.Declare(String,"")
	V.Local.sPart.Declare(String,"")
	V.Local.sRev.Declare(String,"")
	
	'bom part/rev browser
	F.Intrinsic.UI.SetBrowserHotTypeAhead(True)
	
	F.Intrinsic.UI.Browser(3000,"",V.Local.sRet)
	
	F.Intrinsic.Control.If(V.Local.sRet,<>,V.Ambient.Cancel)
		F.Intrinsic.String.Split(V.Local.sRet,"*!*",V.Local.sRet)
		'splitting part/rev 
		F.Intrinsic.String.RPad(V.Local.sRet(0)," ",20,V.Local.sRet(0))
		F.Intrinsic.String.Left(V.Local.sRet(0),17,V.Local.sPart)
		F.Intrinsic.String.Right(V.Local.sRet(0),3,V.Local.sRev)
		Gui.Form_ParentExlusion.txtPart.Text(V.Local.sPart.Trim)
		Gui.Form_ParentExlusion.txtRev.Text(V.Local.sRev.Trim)
		
		Gui.Form_ParentExlusion.cmdAdd.SetFocus
		
	F.Intrinsic.Control.EndIf
	
	F.Intrinsic.Control.ExitSub

F.Intrinsic.Control.Catch
	F.Intrinsic.Control.If(V.Ambient.ErrorNumber,<>,0)
		F.Intrinsic.String.Build("Project: {0}{1}Subroutine: {2}{1}Error Occurred {3} with description {4}","GCG_4447_Bom_Part_Exclusion.g2u",V.Ambient.NewLine,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Local.sError)
		F.Intrinsic.UI.Msgbox(V.Local.sError)
		F.Intrinsic.Control.CallSub(Form_ParentExlusion_UnLoad)
	F.Intrinsic.Control.EndIf
Function.Intrinsic.Control.EndTry
Program.Sub.cmdPart_Click.End

Program.Sub.Remove_Part.Start
F.Intrinsic.Control.Try
	V.Local.sError.Declare(String,"")
	
	'deleint part/rev from datatable
	F.Intrinsic.Control.If(V.Global.iIndex,=,-1)
		F.Intrinsic.Control.ExitSub
	F.Intrinsic.Control.EndIf
	
	F.Data.DataTable.DeleteRow("Part",V.Global.iIndex)
	
	V.Global.iIndex.Set(-1)
	
	F.ODBC.Connection!Con.OpenCompanyConnection
	'saving change to pervasive table
	F.Data.DataTable.SaveToDB("Part","Con","GCG_4447_Bom_Prt_Exc","Part*!*Rev",7,"Part@!@Part*!*Rev@!@Rev")
	F.Data.DataTable.AcceptChanges("Part")
	
	F.ODBC.Connection!Con.Close
	'clearing rowstates in the datatable
	F.Data.DataTable.AcceptChanges("Part")
	
	F.Intrinsic.Control.ExitSub

F.Intrinsic.Control.Catch
	F.Intrinsic.Control.If(V.Ambient.ErrorNumber,<>,0)
		F.Intrinsic.String.Build("Project: {0}{1}Subroutine: {2}{1}Error Occurred {3} with description {4}","GCG_4447_Bom_Part_Exclusion.g2u",V.Ambient.NewLine,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Local.sError)
		F.Intrinsic.UI.Msgbox(V.Local.sError)
		F.Intrinsic.Control.CallSub(Form_ParentExlusion_UnLoad)
	F.Intrinsic.Control.EndIf
Function.Intrinsic.Control.EndTry
Program.Sub.Remove_Part.End

Program.Sub.GsGridControl1_MouseCellEnter.Start
F.Intrinsic.Control.Try
	V.Local.sError.Declare(String,"")
	V.Local.iRet.Declare(Long,0)
	
	'if removepart is in the callstack exitsub and not update the global variable holding the rowindex
	F.Intrinsic.Control.IsInCallstack("Remove_Part",V.Local.iRet)
	
	F.Intrinsic.Control.If(V.Local.iRet,>,0)
		F.Intrinsic.Control.ExitSub
	F.Intrinsic.Control.EndIf
	
	V.Global.iIndex.Set(V.Args.Row)
	
	F.Intrinsic.Control.ExitSub

F.Intrinsic.Control.Catch
	F.Intrinsic.Control.If(V.Ambient.ErrorNumber,<>,0)
		F.Intrinsic.String.Build("Project: {0}{1}Subroutine: {2}{1}Error Occurred {3} with description {4}","GCG_4447_Bom_Part_Exclusion.g2u",V.Ambient.NewLine,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Local.sError)
		F.Intrinsic.UI.Msgbox(V.Local.sError)
		F.Intrinsic.Control.CallSub(Form_ParentExlusion_UnLoad)
	F.Intrinsic.Control.EndIf
Function.Intrinsic.Control.EndTry
Program.Sub.GsGridControl1_MouseCellEnter.End

Program.Sub.Create_ContextMenu.Start
F.Intrinsic.Control.Try
	V.Local.sError.Declare(String,"")
	
	'creating context menu
	Gui.Form_ParentExlusion..ContextMenuCreate("RemovePart")
	'adding the menu items
	Gui.Form_ParentExlusion..ContextMenuAddItem("RemovePart","Item 0",0)
	'setting item text
	Gui.Form_ParentExlusion..ContextMenuSetItemText("RemovePart","Item 0","Remove Part")
	'attaching menu to subroutine
	Gui.Form_ParentExlusion..ContextMenuSetItemEventHandler("RemovePart","Item 0",Remove_Part)
	'setting menu to gridcontrol
	Gui.Form_ParentExlusion.GsGridControl1.ContextMenuAttach("RemovePart")
	
	F.Intrinsic.Control.ExitSub

F.Intrinsic.Control.Catch
	F.Intrinsic.Control.If(V.Ambient.ErrorNumber,<>,0)
		F.Intrinsic.String.Build("Project: {0}{1}Subroutine: {2}{1}Error Occurred {3} with description {4}","GCG_4447_Bom_Part_Exclusion.g2u",V.Ambient.NewLine,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Local.sError)
		F.Intrinsic.UI.Msgbox(V.Local.sError)
		F.Intrinsic.Control.CallSub(Form_ParentExlusion_UnLoad)
	F.Intrinsic.Control.EndIf
Function.Intrinsic.Control.EndTry
Program.Sub.Create_ContextMenu.End