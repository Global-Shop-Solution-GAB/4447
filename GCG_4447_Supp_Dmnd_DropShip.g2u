Program.Sub.Preflight.Start
Program.Sub.Preflight.End

Program.Sub.Main.Start
F.Intrinsic.Control.Try

V.Local.sError.Declare(String)

F.ODBC.Connection!con.OpenConnection(V.Ambient.PDSN,V.Ambient.PUser,V.Ambient.PPass)

F.Intrinsic.Control.If(V.Caller.Hook,=,14055)
	F.Intrinsic.Control.CallSub(getdata)
F.Intrinsic.Control.EndIf

F.Intrinsic.Control.CallSub(exit)

F.Intrinsic.Control.ExitSub

F.Intrinsic.Control.Catch
F.Intrinsic.Control.If(V.Ambient.ErrorNumber,<>,0)
	F.Intrinsic.String.Build("Project: {0}{1}Subroutine: {2}{1}Error Occurred {3} with description {4}","GCG_4447_Supp_Dmnd_DropShip.g2u",V.Ambient.NewLine,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Local.sError)
	F.Intrinsic.UI.Msgbox(V.Local.sError)
	F.Intrinsic.Control.CallSub(exit)
F.Intrinsic.Control.EndIf
Function.Intrinsic.Control.EndTry
Program.Sub.Main.End

Program.Sub.Exit.Start
F.Intrinsic.Control.Try

V.Local.sError.Declare(String)

F.ODBC.Connection!con.close
F.Intrinsic.Control.End

F.Intrinsic.Control.ExitSub

F.Intrinsic.Control.Catch
F.Intrinsic.Control.If(V.Ambient.ErrorNumber,<>,0)
	F.Intrinsic.String.Build("Project: {0}{1}Subroutine: {2}{1}Error Occurred {3} with description {4}","GCG_4447_Supp_Dmnd_DropShip.g2u",V.Ambient.NewLine,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Local.sError)
	F.Intrinsic.UI.Msgbox(V.Local.sError)
	F.Intrinsic.Control.End
F.Intrinsic.Control.EndIf
Function.Intrinsic.Control.EndTry
Program.Sub.Exit.End

Program.Sub.getdata.Start
F.Intrinsic.Control.Try

V.Local.sError.Declare(String)
V.Local.iRowCnt.Declare(Long)
V.Local.sReadRow.Declare(String)
V.Local.i.Declare(Long)
V.Local.sPart.Declare(String)
V.Local.sSql.Declare(String)
V.Local.sLine.Declare(String)
V.Local.sTitle.Declare(String)
V.Local.sSo.Declare(String)
V.Local.sPo.Declare(String)
V.Local.sPoLine.Declare(String)
V.Local.sSoLine.Declare(String)
V.Local.sTemp.Declare(String)

'supply grid
F.Intrinsic.BDF.Load("AUX001","AUX001")
F.Intrinsic.BDF.Clone("AUX001","CLONE")

F.Intrinsic.BDF.SetColumnTitle("CLONE",7,"SO/Line Drop Ship]=[PW::150]=[]=[CA::7]=[")

F.Intrinsic.BDF.ReadRowCount("AUX001",V.Local.iRowCnt)

F.Intrinsic.Math.Sub(V.Local.iRowCnt,1,V.Local.iRowCnt)
F.Intrinsic.Control.For(V.Local.i,0,V.Local.iRowCnt,1)
	F.Intrinsic.BDF.ReadRow("AUX001",V.Local.i,V.Local.sReadRow)
	F.Intrinsic.String.Split(V.Local.sReadRow,"|~|",V.Local.sReadRow)

	F.Intrinsic.String.Left(V.Local.sReadRow(0),7,V.Local.sPo)
	F.Intrinsic.String.Mid(V.Local.sReadRow(0),8,4,V.Local.sPoLine)
	F.Intrinsic.String.RPad(V.Local.sPoLine,"0",4,V.Local.sPoLine)
	F.Intrinsic.String.Build("Select Order_No + '-' + Order_Line as SO from V_PO_Drop_Ship where PO = '{0}' and PO_Line = '{1}'",V.Local.sPo,V.Local.sPoLine,V.Local.sSql)
	F.ODBC.Connection!con.OpenLocalRecordsetRO("rst",v.Local.sSql)
	
	F.Intrinsic.Control.If(V.ODBC.con!rst.EOF,=,False)
		F.Intrinsic.String.LPad(V.ODBC.con!rst.FieldVal!SO,"0",12,V.Local.sTemp)
		V.Local.sReadRow(7).Set(V.Local.sTemp)
	F.Intrinsic.Control.Else
		V.Local.sReadRow(7).Set("")
	F.Intrinsic.Control.EndIf
	F.ODBC.con!rst.Close
	F.Intrinsic.String.Join(V.Local.sReadRow,"|~|",V.Local.sLine)
	
	F.Intrinsic.BDF.WriteRow("CLONE",V.Local.sLine)
	
F.Intrinsic.Control.Next(V.Local.i)

F.Intrinsic.BDF.Save("CLONE","AUX001")

V.Local.sPo.Set("")
V.Local.sPoLine.Set("")

'demand grid
F.Intrinsic.BDF.Load("AUX002","AUX002")
F.Intrinsic.BDF.Clone("AUX002","CLONE2")

F.Intrinsic.BDF.SetColumnTitle("CLONE2",13,"PO/Line Drop Ship]=[PW::150]=[]=[CA::7]=[")

F.Intrinsic.BDF.ReadRowCount("AUX002",V.Local.iRowCnt)

F.Intrinsic.Math.Sub(V.Local.iRowCnt,1,V.Local.iRowCnt)
F.Intrinsic.Control.For(V.Local.i,0,V.Local.iRowCnt,1)
	F.Intrinsic.BDF.ReadRow("AUX002",V.Local.i,V.Local.sReadRow)
	F.Intrinsic.String.Split(V.Local.sReadRow,"|~|",V.Local.sReadRow)

	F.Intrinsic.String.Left(V.Local.sReadRow(0),7,V.Local.sSo)
	F.Intrinsic.String.Mid(V.Local.sReadRow(0),8,4,V.Local.sSoLine)
	F.Intrinsic.String.LPad(V.Local.sSoLine.Trim,"0",3,V.Local.sSoLine)
	F.Intrinsic.String.RPad(V.Local.sSoLine,"0",4,V.Local.sSoLine)
	F.Intrinsic.String.Build("Select PO + '-' + PO_Line as PO from V_PO_Drop_Ship where Order_NO = '{0}' and Order_Line = '{1}'",V.Local.sSo,V.Local.sSoLine,V.Local.sSql)
	F.ODBC.Connection!con.OpenLocalRecordsetRO("rst",v.Local.sSql)
	
	F.Intrinsic.Control.If(V.ODBC.con!rst.EOF,=,False)
		F.Intrinsic.String.LPad(V.ODBC.con!rst.FieldVal!PO,"0",12,V.Local.sTemp)
		V.Local.sReadRow(13).Set(V.Local.sTemp)
	F.Intrinsic.Control.Else
		V.Local.sReadRow(13).Set("")
	F.Intrinsic.Control.EndIf
	F.ODBC.con!rst.Close
	F.Intrinsic.String.Join(V.Local.sReadRow,"|~|",V.Local.sLine)
	
	F.Intrinsic.BDF.WriteRow("CLONE2",V.Local.sLine)
	
F.Intrinsic.Control.Next(V.Local.i)

F.Intrinsic.BDF.Save("CLONE2","AUX002")

F.Intrinsic.Control.ExitSub

F.Intrinsic.Control.Catch
F.Intrinsic.Control.If(V.Ambient.ErrorNumber,<>,0)
	F.Intrinsic.String.Build("Project: {0}{1}Subroutine: {2}{1}Error Occurred {3} with description {4}","GCG_4447_Supp_Dmnd_DropShip.g2u",V.Ambient.NewLine,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Local.sError)
	F.Intrinsic.UI.Msgbox(V.Local.sError)
	F.Intrinsic.Control.CallSub(exit)
F.Intrinsic.Control.EndIf
Function.Intrinsic.Control.EndTry
Program.Sub.getdata.End

